buildscript {
    repositories {
        mavenCentral()
        maven {
            url "${artifactory_contextUrl}plugins-release-local"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath 'com.precognox.gradle:gradle-java-dropwizard:+'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
    id "com.jfrog.artifactory" version "3.0.1"
    id "com.bmuschko.docker-java-application" version "2.3.1"
}

project.ext.dockerBaseImage = "${registry_url}/common/jdk8:0.1.2-slim"
project.ext.jenkins_job='active-publisher-tracker-active-publisher-tracker-backend'
project.ext.basename='active-publisher-tracker-backend'
project.group 'active-publisher-tracker'
project.ext.mainClassName = 'com.precognox.publishertracker.PublisherTrackerApplication'

project.ext.dockerDefaultCommand = ['bash', '-c', "dockerize -template " +
        "/${rootProject.name}/config.yml.template:/${rootProject.name}/config.yml " +
        "/${rootProject.name}/bin/${rootProject.name} server /${rootProject.name}/config.yml"
]

apply plugin: 'idea'
apply plugin: 'gradle-java-dropwizard'

configurations.all {
    resolutionStrategy {
        force 'org.slf4j:slf4j-api:1.7.25'
    }
}

dependencies {
    //dropwizard
    compile 'javax.ws.rs:javax.ws.rs-api:2.0'
    compile 'io.dropwizard:dropwizard-core:1.0.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.6'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    //DB
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.1.0'
    compile 'common:dropwizard-ebean:0.1.0'
    compile 'org.avaje.ebean:ebean-agent:8.2.1'
    compile('io.dropwizard:dropwizard-migrations:1.1.0') {
        exclude group: 'io.dropwizard', module: 'dropwizard-db'
    }

    compile 'common-services:email-delivery-service-interface:0.1.0-development-SNAPSHOT'

    compile 'tas:user-management-common:1.0.1'
    compile 'org.secnod.dropwizard:dropwizard-shiro:0.2.0'

    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    compile 'commons-io:commons-io:2.6'
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'javax.mail', name: 'javax.mail-api', version: '1.5.6'
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.6'
    
    testCompile 'junit:junit:4.12'
    testCompile 'com.h2database:h2:1.4.193'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

// ebean enchancement
classes.doLast {
    javaexec {
        classpath = sourceSets.main.compileClasspath
        main = 'com.avaje.ebean.enhance.ant.MainTransform'
        args = [
                compileJava.destinationDir,
                'com.precognox.publishertracker.entities',
                'debug=1'
        ]
    }
}
